// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

buildscript {
    ext {
        buildToolsVersion = "29.0.2"
        minSdkVersion = 28
        compileSdkVersion = 31
        targetSdkVersion = 31
        firebaseMessagingVersion = "21.1.0"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:4.1.0')
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            url 'https://pacify.jfrog.io/artifactory/android/'
            name 'Pacify'
        }
        google()
        jcenter()
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    ext {
        def packageVersion = getPackageVersions()
        versionMajor = packageVersion[0]
        versionMinor = packageVersion[1]
        versionPoint = packageVersion[2]
    }
}

def getPackageVersions() {
    def (major, minor, point) = getPackageVersion().tokenize('.')
    return [Integer.parseInt(major), Integer.parseInt(minor), Integer.parseInt(point)] as int[]
}

def getPackageVersion() {
    def packageJson = new File("../package.json")
    def packageVersion = new JsonSlurper().parseText(packageJson.text)
    return packageVersion["version"]
}

